/*
 * Copyright (c) 2015. Ronald D. Kurr kurr@jvmguy.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'org.springframework.boot'

// add Spring Cloud to the mix
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// sometimes, other projects want to use the artifact as a dependency, so we'll make TWO jars
bootRepackage  {
    classifier = 'executable'
}

// have Gradle expand properties when copying resource files
processResources {
    filesMatching( '**/application.yml' ) {
        expand( project.properties )
    }
    filesMatching( '**/bootstrap.yml' ) {
        expand( project.properties )
    }
}

springBoot  {
    // enable a self-executing jar .eg ./my-jar.jar
    executable = true

    // attributes to expose via the /info actuator
    buildInfo {
        additionalProperties = [
                'name': "${project.name}",
                'description': "${project.description}",
                'built-on.date': "${project.builtOn}",
                'build-using.groovy.version': "${project.groovyVersion}",
                'build-using.java.version': System.properties['java.version'],
                'build-using.java.vendor': System.properties['java.vendor'],
                'built-on.os.name': System.properties['os.name'],
                'built-on.os.arch': System.properties['os.arch'],
                'built-on.os.version': System.properties['os.version']
        ]
    }
}
